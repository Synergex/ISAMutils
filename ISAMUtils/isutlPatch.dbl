;; ***************************************************************************
;;
;;  Title:      isutlPatch.dbl
;;
;;  Type:       UserControl
;;
;;  Description: UserControl to be inserted into ISUTL -> Patch Tab
;;              
;;  Author:     Jerry Fawcett, Synergy/DE Developer Support
;;
;;  Copyright:  ©Synergex International Inc.  All rights reserved.
;;
;;  WARNING:    If you were given this code by a Synergex employee then you may
;;              use and modify it freely to generate code for your applications.
;;              However, you may not under any circumstances distribute this
;;              code, or any modified version of this code, to any third party
;;              without first obtaining written permission to do so from
;;              Synergex.  In using this code you accept that it is provided
;;              as is, and without support or warranty.  Neither Synergex or the
;;              author accept any responsibility for any losses or damages of
;;              any nature which may arise from the use of this code.  This
;;              header information must remain, unaltered in the code at all
;;              times.  Possession of this code, or any modified version of this
;;              code, indicates your acceptance of these terms.
;;
;; ***************************************************************************
import System
import System.Collections.Generic
import System.ComponentModel
import System.Data
import System.Drawing
import System.Windows.Forms

namespace ISAMUtils

	public partial class isutlPatch extends System.Windows.Forms.UserControl implements Ioptionspage
		
		public method isutlPatch
			endparams
		proc
			try
			begin
				this.InitializeComponent()
				comboxISUTLPatchRevisionLevel.SelectedIndex = 0
			end

			catch(synerr)
			begin
				generalRoutines.catch_code(synerr.ToString(),synerr.source.ToString())
			end
			endtry
		endmethod

;;;***********************************************************************************
;;; Button Clicks
;;; calls generalRoutines to handle folder and file browse button clicks
;;;***********************************************************************************		
		private method btnISUTLPatchBrowseISAM_Click, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams
		proc
			generalRoutines.fileBrowseBtn(this.tboxISUTLPatchBrowseISAM,1)
		endmethod		
		

		private method comboxISUTLPatchRevisionLevel_SelectedIndexChanged, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams

			record
				index	,a1
				elem	,@String
			endrecord
		proc
			try
			begin
				index = comboxISUTLPatchRevisionLevel.SelectedIndex
				using index select
				('1'),
				begin
					elem = "2 "
				end
				('2'),
				begin
					elem = "3 "
				end
				('3'),
				begin
					elem = "5 "
				end
				(),
				begin
					clear elem
				end
				endusing
				comboxISUTLPatchRevisionLevel.Tag = elem
			end

			catch(synerr)
			begin
				generalRoutines.catch_code(synerr.ToString(),synerr.source.ToString())
			end
			endtry
		endmethod
		

		private method cboxIsutlPatchQfile_CheckedChanged, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams
		proc
			generalRoutines.clearPanel(panIsutlPatchQfile, cboxIsutlPatchQfile)
			panIsutlPatchQfile.Focus()
		endmethod

;;;***********************************************************************************
;;; Leave methods for required fields
;;; validates if a required field has data in it or not
;;;***********************************************************************************
		private method panIsutlPatchQfile_Leave, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams
		proc
			generalRoutines.validatefield("the 'Set or unset the network encryption flag'",cboxIsutlPatchQfile,panIsutlPatchQfile)
		endmethod

;;;***********************************************************************************
;;; properties for Ioptionspage interface -- SCL
;;;***********************************************************************************
		public property Options, @List<string>
			method get
			proc
				mreturn generalRoutines.bldCmdLine(this)
			endmethod
		endproperty
	endclass
endnamespace