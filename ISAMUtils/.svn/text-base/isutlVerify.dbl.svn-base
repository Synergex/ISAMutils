;; ***************************************************************************
;;
;;  Title:      isutlVerify.dbl
;;
;;  Type:       UserControl
;;
;;  Description:UserControl for ISUTL -v controls
;;              
;;  Author:     Dean Knipping, Synergy/DE Developer Support
;;
;;  Copyright:  ©Synergex International Inc.  All rights reserved.
;;
;;  WARNING:    If you were given this code by a Synergex employee then you may
;;              use and modify it freely to generate code for your applications.
;;              However, you may not under any circumstances distribute this
;;              code, or any modified version of this code, to any third party
;;              without first obtaining written permission to do so from
;;              Synergex.  In using this code you accept that it is provided
;;              as is, and without support or warranty.  Neither Synergex or the
;;              author accept any responsibility for any losses or damages of
;;              any nature which may arise from the use of this code.  This
;;              header information must remain, unaltered in the code at all
;;              times.  Possession of this code, or any modified version of this
;;              code, indicates your acceptance of these terms.
;;
;; ***************************************************************************
import System
import System.Collections.Generic
import System.ComponentModel
import System.Data
import System.Drawing
import System.Windows.Forms

namespace ISAMUtils

	public partial class isutlVerify extends System.Windows.Forms.UserControl implements Ioptionspage
		
		public method isutlVerify
			endparams

		proc
			try
			begin
				this.InitializeComponent()
				;;;sets default value for message level
				comboxISUTLVerifyMessage.SelectedIndex = 0
			end

			catch(synerr)
			begin
				generalRoutines.catch_code(synerr.ToString(),synerr.source.ToString())
			end
			endtry
		endmethod

;;;***********************************************************************************
;;; Button Clicks
;;; calls generalRoutines to handle folder and file browse button clicks
;;;***********************************************************************************	
		private method btnISUTLVerifyBrowseISAM_Click, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams

		proc
			generalRoutines.fileBrowseBtn(tboxISUTLVerifyBrowseISAM,1)
		endmethod

		
		private method btnISUTLVerifyBrowseTemp_Click, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams

		proc
			generalRoutines.dirBrowseBtn(tboxISUTLVerifyBrowseTemp)
		endmethod

;;;***********************************************************************************
;;; CheckBox CheckedChanged - Clear Panel
;;; when the checkBox is either checked or unchecked, these routines will fire and
;;; clear out the data in the fields associated with that checkBox
;;;***********************************************************************************		
		private method cboxISUTLVerifyT_CheckedChanged, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams

		proc
			generalRoutines.clearPanel(panISUTLVerifyBrowseTemp,cboxISUTLVerifyT)
			tboxISUTLVerifyBrowseTemp.Focus()
		endmethod

;;;***********************************************************************************
;;; ComboBox selection changes
;;; sets the tag value for the combo boxes based on what the SelectedItemIndex is	
;;;***********************************************************************************
		private method comboxISUTLVerifyMessage_SelectedIndexChanged, void
			byval sender, @System.Object
			byval e, @System.EventArgs
			endparams

			record
				elem	,@String
				index	,a1
			endrecord

		proc
			try
			begin
				index = comboxISUTLVerifyMessage.SelectedIndex
				using index select
				('1'),
				begin
					elem = " -m0 "
					comboxISUTLVerifyMessage.Tag = elem
				end
				('2'),
				begin
					elem = " -m1 "
					comboxISUTLVerifyMessage.Tag = elem
				end
				('3'),
				begin
					elem = " -m2 "
					comboxISUTLVerifyMessage.Tag = elem
				end
				('4'),
				begin
					elem = " -m3 "
					comboxISUTLVerifyMessage.Tag = elem
				end
				(),
				begin
					clear elem
					comboxISUTLVerifyMessage.Tag = elem
				end
				endusing
			end

			catch(synerr)
			begin
				generalRoutines.catch_code(synerr.ToString(),synerr.source.ToString())
			end
			endtry
		endmethod

;;;***********************************************************************************
;;; properties for Ioptionspage interface -- SCL
;;;***********************************************************************************
		public property Options, @List<string>
			method get

			proc
				mreturn generalRoutines.bldCmdLine(this)
			endmethod
		endproperty
	endclass
endnamespace